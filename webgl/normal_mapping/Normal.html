<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>normal mapping by king_yin3613 </title>
<meta charset="utf-8"> 
</head>

<script id="shader-vs" type="x-shader/x-vertex">
//geometry
attribute vec3 aVertexPosition;
attribute vec3 aVertexNormal;
attribute vec3 aVertexTangent;
attribute vec4 aVertexColor;
attribute vec2 aVertexTextureCoords;
 
//matrices
uniform mat4 uMVMatrix;
uniform mat4 uPMatrix;
uniform mat4 uNMatrix;
 
//lights
uniform vec3 uLightPosition;
 
//varyings
varying vec2 vTextureCoord;
varying vec3 vTangentLightDir;
varying vec3 vTangentEyeDir;
 
void main(void) {
    //Transformed vertex position
    vec4 vertex = uMVMatrix * vec4(aVertexPosition, 1.0);
 
    //Transformed normal position
    vec3 normal = vec3(uNMatrix * vec4(aVertexNormal, 1.0));
    vec3 tangent = vec3(uNMatrix * vec4(aVertexTangent, 1.0));
    vec3 bitangent = cross(normal, tangent);
 
    mat3 tbnMatrix = mat3(
        tangent.x, bitangent.x, normal.x,
        tangent.y, bitangent.y, normal.y,
        tangent.z, bitangent.z, normal.z
    );
 
    //light direction, from light position to vertex
    vec3 lightDirection = uLightPosition - vertex.xyz;
 
    //eye direction, from camera position to vertex
    vec3 eyeDirection = -vertex.xyz;
 
    //Final vertex position
    gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
    vTextureCoord = aVertexTextureCoords;
    vTangentLightDir = lightDirection * tbnMatrix;
    vTangentEyeDir = eyeDirection * tbnMatrix;
}
</script>
    
<script id="shader-fs" type="x-shader/x-fragment">
precision mediump float;
 
//geometry
uniform vec4 uMaterialDiffuse;
uniform vec4 uMaterialAmbient;
 
uniform vec4 uLightAmbient;
uniform vec4 uLightDiffuse;
 
//samplers
uniform sampler2D uSampler;
uniform sampler2D uNormalSampler;
 
//varying
varying vec4 vColor;
varying vec2 vTextureCoord;
varying vec3 vTangentLightDir;
varying vec3 vTangentEyeDir;
 
void main(void)
{   
    // Unpack tangent-space normal from texture
    vec3 normal = normalize(2.0 * (texture2D(uNormalSampler, vTextureCoord).rgb - 0.5));
 
    // Normalize the light direction and determine how much light is hitting this point
    vec3 lightDirection = normalize(vTangentLightDir);
    float lambertTerm = max(dot(normal,lightDirection),0.10);
 
    // Calculate Specular level
    vec3 eyeDirection = normalize(vTangentEyeDir);
    vec3 reflectDir = reflect(-lightDirection, normal);
    float Is = pow(clamp(dot(reflectDir, eyeDirection), 0.0, 1.0), 4.0);
 
    // Combine lighting and material colors
    vec4 Ia = uLightAmbient * uMaterialAmbient;
    vec4 Id = uLightDiffuse * uMaterialDiffuse * texture2D(uSampler, vTextureCoord) * lambertTerm;
    
    gl_FragColor = Ia + Id + Is;
}
</script>

<script type='text/javascript' src="gl-matrix-min.js"></script> 
<script type='text/javascript' src="webgl-utils.js"></script>
<script type='text/javascript' src="Normal.js"></script>
<script type='text/javascript' src='Camera.js'></script>
<script type='text/javascript' src='CameraInteractor.js'></script>
<script type='text/javascript' src='Program.js'></script>
<script type='text/javascript' src='Scene.js'></script>
<script type='text/javascript' src='Utils.js'></script>

<body onLoad="startup();" scroll="no">

<h3 class="title" style="font-size: 1.6em; margin: 0px auto; line-height: 1.2em; font-family: Arial, Helvetica, sans-serif; background-color: rgb(255, 255, 255);">WebGL: Normal Mapping </h3>

<canvas id="canvas-element-id" width="640" height="360"></canvas>

<div class="content-middle" style="font-family: Arial, Helvetica, sans-serif; font-size: 13.44px; line-height: 20.16px; background-color: rgb(255, 255, 255);">
    <div class="node" style="margin: 0.5em 0px 1em; padding-bottom: 1em; border-bottom-width: 1px; border-bottom-style: solid; border-bottom-color: rgb(187, 187, 187); clear: both;">
    <div class="content" style="margin: 0.5em 0px;">
    <p>Drag your mouse on the canvas to rotate the view. Use arrow keys to rotate the sphere only. </p>
    <p><b>Note</b>: node.js might be needed to load local files, please refer to Readme.txt in the working directory for the instruction. </p>
    </div>
    </div>
    </div>

</body>
</html> 